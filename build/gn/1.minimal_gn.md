# Gn 最小配置

一个 GN 构建项目至少包括

```
.
├── BUILD.gn               # 编译入口点
├── .gn                    # .gn 项目根编译配置。指定配置文件目录地方，就是这个文件指定了 ‘build/BUILDCONFIG.gn’ 作为配置文件。
├── build                  # 配置目录，必须有
│   ├── BUILD.gn
│   ├── BUILDCONFIG.gn     # 配置文件，必须有
│   └── toolchain
│       └── BUILD.gn
├── 代码
|  ...
```

`BUILD.gn` 是构建的入口点。

## 编译类型

编译的类型有可执行程序和库(共享库、静态库)。分别对应于：

```
# 创建可执行程序
executable("tutorial") {
  sources = [
    "tutorial.cc",
  ]
}

# 编译为共享库
shared_library("tutorial") {
  # 执行代码。
  sources = [
    "hello_shared.cc",
    "hello_shared.h",
  ]
}

# 编译为静态库
static_library("tutorial") {
  sources = [
    "hello_static.cc",
    "hello_static.h",
  ]
}
```

## 依赖

添加依赖可以在任何 `executable`、`execshared_libraryutable`、`static_library`的作用域内使用 `deps` 数组，如 `executable`

```
executable("hello") {
  sources = [
    "hello.cc",
  ]

  deps = [
    ":hello_shared",
    ":hello_static",
  ]
}
```

## group 

在GN中，组只是没有编译或链接的依赖项的集合

```
group("tools") {
  deps = [
    # This will expand to the name "//tutorial:tutorial" which is the full name
    # of our new target. Run "gn help labels" for more.
    "//tutorial",
  ]
}
```


## config 

默认情况下，每个构建目标都有一些适用于的构建设置。 这些设置通常默认配配置列表。 可以使用 “print” 命令查看此信息，这对于调试非常有用：

```
executable("hello") {
  ...
  print(configs)
}
```

```
$ gn gen out
["//build:compiler_defaults", "//build:executable_ldconfig"]
Done. Made 5 targets from 5 files in 9ms
```

也可以在用字符串中格式化变量。 它使用符号 "$" 来引用变量：
```
print("The configs for the target $target_name are $configs")
```

### 添加自定义配置

除了系统内置的，我们也能添加自定义的配置。

```
config("my_lib_config") {
  defines = [ "ENABLE_DOOM_MELON" ]
  include_dirs = [ "//third_party/something" ]
}
```

要将配置的设置应用于目标，只需将其添加到 `configs` 列表中：

```
static_library("hello_shared") {
  ...
  # Note "+=" here is usually required, see "default configs" below.
  configs += [
    ":my_lib_config",
  ]
}
```

通过将配置的标签放在public_configs列表中，可以将配置应用于依赖于当前配置的所有目标：

```
static_library("hello_shared") {
  ...
  public_configs += [
    ":my_lib_config",
  ]
}
```

公共配置也适用于当前目标，所以没有必要在这两个地方列出配置。

#### 添加和删除配置

可以使用 `+=`、`-=` 分别添加和删除配置。

```
executable("hello") {
  ...
  configs -= [ "//build:no_exceptions" ]  # Remove global default.
  configs += [ "//build:exceptions" ]  # Replace with a different one.
}
```

查看有哪些系统 `config` 可用，可以查看项目中配置文件 `build/BUILD.gn` 的 `config` 命令定义的配置。


## 构建参数

gn 可以任意指定构建目录，那如何区分 Debug版本和Release版本呢？GN通过传递参数来解决。也就是说，现在只通过输出目录是无法确定到底是Debug版本和Release版本，而要取决于传递的构建参数。使用如下命令参加参数。
构建参数用于指定 gn 构建脚本可以配置的部分。例如目标平台，调试或者release。

```shell
$ gn gen out --args='target_os="mac"'
```

构建参数会在输出目录生成 `args.gn` 文件。例如上面的命令生成在 `out/args.gn` 文件。
```
# out/args.gn
target_os = "mac"
```

虽然知道在 `args.gn` 目录下，但是 out 目录是输出目录，最好不要直接编辑。你可以使用 gn args 命令生成。

```shell
$ gn args out
```

该指令会直接打开一个编辑器，用于编辑。你可以以键值对的方式添加构建参数。添加的格式是：

```
<key> = <value>
```

如：

```
is_debug = false
```

如果你不知道有哪些参数可以使用，可以使用下面的命令列出可用的构建参数和它们的缺省值：

```shell
$ gn args --list out
```

### 自定义构建参数

如果你在自己编写构建脚本时，除了系统的构建参数外，也可以自定义构建参数。使用 declare_args 命令在 `BUILD.gn` 文件中添加

```c
declare_args() {
  enable_teleporter = true
  enable_doom_melon = false
}

if (enable_teleporter) {
  print("enable_teleporter is true")
} else {
  print("enable_teleporter is false")
}
```

这里使用 `enable_teleporter` 控制输出的内容。除了这里设置的默认值，你还可以使用 `gn gen out --args=XXX` 命令行或者 `gn args out` 命令覆盖 `BUILD.gn` 的默认值。其优先级为

```
--args=XXX 命令参数  >  args.gn 文件 >  BUILD.gn 的默认值
```

需要说明的是，指定 `--args` 参数形式会覆盖 `args.gn` 文件，如果不添加 `--args` 参数则不会覆盖 `args.gn` 文件。


## 定义宏

构建目标的作用域内可以使用 `defines` 定义源码中要使用的宏。如

```c
shared_library("hello_shared") {
  sources = [
    "hello_shared.cc",
    "hello_shared.h",
  ]

  defines = [
    "HELLO_SHARED_IMPLEMENTATION",
    "ENABLE_DOOM_MELON=0",
  ]
}
```

## 构建流程如何？

如果你想要查看构建的详细流程，可以在详细模式()下运行 gn，以查看有关其操作的大量消息。 为此使用 `-v` 参数。


### desc 子命令

您可以运行 `gn desc <build_dir> <targetname>`来获取有关给定目标的信息：

```
$ gn desc out //:hello
$ gn desc out //tutorial:tutorial
```



